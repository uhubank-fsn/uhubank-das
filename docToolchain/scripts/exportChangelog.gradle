//tag::exportChangeLog[]
task exportChangeLog(
        description: 'exports the change log from a git subpath',
        group: 'docToolchain'
) {
    doFirst {
        new File(targetDir).mkdirs()
    }
    doLast {
        println "changelogConfigFile: ${changelogConfigFile}"
        def config
        config = new ConfigSlurper().parse(new File(docDir, changelogConfigFile).text)

        def cmd = "${config.changelogCmd} ."
        def changes = cmd.execute(null, new File(docDir, config.changelogDir)).text
        def changelog = new File(targetDir, 'changelog.adoc')
        logger.info "> changelog exported ${changelog.canonicalPath}"
        changelog.write(changes)
    }
}
//end::exportChangeLog[]
