/*
 * This build file is part of the docToolchain
 */

plugins {
    id "org.asciidoctor.convert" version "1.5.3"
    id "org.aim42.htmlSanityCheck" version "1.0.0-RC-2"
    id "com.github.ben-manes.versions" version "0.15.0"
}

apply plugin:'groovy'

dependencies {
    testCompile(
            ('junit:junit:4.12'),
            ('org.spockframework:spock-core:1.1-groovy-2.4'),
            ('com.athaydes:spock-reports:1.3.2'),
            ('org.slf4j:slf4j-api:1.7.13'),
            ('org.slf4j:slf4j-simple:1.7.13'),
            gradleTestKit()
    )
}

if (project.name!=rootProject.name) {
    // disable all tasks which are not docToolchain tasks
    // this avoids for example that task 'check' runs if invoked in main project
    gradle.taskGraph.whenReady {
        tasks.each { task ->
            if (task.group in ['docToolchain', 'Documentation']
                    || task.name in ['htmlSanityCheck', 'streamingExecute', 'tasks', 'clean']) {
                task.enabled = true
            } else {
                task.enabled = false
            }
        }
    }
}

//all available docToolchain modules
//the first is mandatory
apply from: 'scripts/AsciiDocBasics.gradle'
//the following are optional
apply from: 'scripts/exportEA.gradle'
apply from: 'scripts/exportPPT.gradle'
apply from: 'scripts/exportVisio.gradle'
apply from: 'scripts/exportChangelog.gradle'
apply from: 'scripts/exportContributors.gradle'
apply from: 'scripts/exportJiraIssues.gradle'
apply from: 'scripts/exportExcel.gradle'
apply from: 'scripts/exportMarkdown.gradle'
apply from: 'scripts/pandoc.gradle'
apply from: 'scripts/publishToConfluence.gradle'
apply from: 'scripts/htmlSanityCheck.gradle'

// let's set a defaultTask for convenience
//defaultTasks 'exportChangeLog','exportJiraIssues','asciidoctor'
defaultTasks 'exportChangeLog', 'exportContributors', 'exportMarkdown', 'generateHTML', 'generatePDF', 'htmlSanityCheck'
